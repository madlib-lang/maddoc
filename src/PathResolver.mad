import List from "List"
import J from "Json"
import { Just, Nothing } from "Maybe"
import { always, any, equals, ifElse } from "Function"
import { drop, split, toLower, match, len } from "String"
import { isRootPathOf, canonicalizePath, FilePath } from "FilePath/Posix"
import {} from "Compare"

import {
  State
} from "./State"
import { Expression } from "./Views/Expression"
import {
  Module,
  Expression,
  Type
} from "./Parser/Documentation"

import IO from "IO"

export type PathResult
  = ModuleResult (List Module)
  | ExpressionResult String Expression
  | TypeResult String Type
  | NotFound


filterByPath :: String -> List Module -> List Module
filterByPath = (path) => {
  canPath = canonicalizePath(path)

  return List.filter((module) =>
    pipe(
      .name,
      toLower,
      ifElse(
        isRootPathOf(drop(1, toLower(canPath))),
        always(
          !List.isEmpty(module.expressions)
          || !List.isEmpty(module.typeDeclarations)
        ),
        pipe(
          always([
            ...map(.name, module.expressions),
            ...map(.name, module.typeDeclarations)
          ]),
          map(pipe(mappend(`${module.name}/`), toLower)),
          any(isRootPathOf(drop(1, toLower(canPath))))
        )
      )
    )(module)
  )
}


getModulesToShow :: State -> List Module
export getModulesToShow = (state) => pipe(
  filterByPath(state.path),
  map((module) => ({
    ...module,
    expressions: List.filter(pipe(.name, match(state.search)), module.expressions)
  }))
)(state.modules)

isItemView :: String -> List Module -> Boolean
isItemView = (path) => ifElse(
  pipe(List.len, equals(1)),
  pipe(
    List.first,
    where
      is Just m : len(canonicalizePath(path)) > len(`/${m.name}`)
      is Nothing: false
  ),
  always(false)
)


findExpression :: FilePath -> Module -> PathResult
findExpression = (path, module) => pipe(
  .expressions,
  List.find((e) => Just(e.name) == List.last(split("/", path))),
  where
    is Just found: ExpressionResult(module.name, found)
    is Nothing   : NotFound
)(module)

findType :: FilePath -> Module -> PathResult
findType = (path, module) => pipe(
  .typeDeclarations,
  List.find((e) => Just(e.name) == List.last(split("/", path))),
  where
    is Just found: TypeResult(module.name, found)
    is Nothing   : NotFound,
  IO.log
)(module)

findItem :: FilePath -> Module -> PathResult
findItem = (path, module) => pipe(
  findExpression(path),
  where
    is NotFound: findType(path, module)
    is a       : a,
  IO.log
)(module)


processPath :: State -> PathResult
export processPath = (state) => pipe(
  getModulesToShow,
  ifElse(
    isItemView(state.path),
    pipe(
      List.first,
      where is Just m: findItem(state.path, m)
    ),
    ModuleResult
  ),
  IO.log
)(state)
