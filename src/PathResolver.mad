import List from "List"
import { Just, Nothing } from "Maybe"
import { always, any, equals, ifElse } from "Function"
import { drop, split, toLower, len } from "String"
import type { FilePath } from "FilePath/Posix"
import { isRootPathOf, canonicalizePath } from "FilePath/Posix"
import {} from "Compare"

import type { State } from "@/State"
import type {
  Module,
  Expression,
  Alias,
  Interface,
  Instance,
  Type
} from "@/Parser/Documentation"



export type PathResult
  = ModuleResult (List Module)
  | ExpressionResult String Expression
  | TypeResult String Type
  | AliasResult String Alias
  | InterfaceResult String Interface
  | InstanceResult String Instance
  | NotFound


filterByPath :: String -> List Module -> List Module
filterByPath = (path) => {
  canPath = canonicalizePath(path)

  return List.filter((module) =>
    pipe(
      .name,
      toLower,
      ifElse(
        isRootPathOf(drop(1, toLower(canPath))),
        always(
          !List.isEmpty(module.expressions)
          || !List.isEmpty(module.typeDeclarations)
          || !List.isEmpty(module.aliases)
          || !List.isEmpty(module.interfaces)
          || !List.isEmpty(module.instances)
        ),
        pipe(
          always([
            ...map(.name, module.expressions),
            ...map(.name, module.typeDeclarations),
            ...map(.name, module.aliases),
            ...map(.name, module.interfaces),
            ...map(.declaration, module.instances)
          ]),
          map(pipe(mappend(`${module.name}/`), toLower)),
          any(isRootPathOf(drop(1, toLower(canPath))))
        )
      )
    )(module)
  )
}


getModulesToShow :: State -> List Module
export getModulesToShow = (state) => pipe(
  .modules,
  filterByPath(state.path)
)(state)

isItemView :: String -> List Module -> Boolean
isItemView = (path) => ifElse(
  pipe(List.len, equals(1)),
  pipe(
    List.first,
    where
      is Just m : len(canonicalizePath(path)) > len(`/${m.name}`)
      is Nothing: false
  ),
  always(false)
)

tryItemByKind :: (a -> b -> PathResult)
  -> (b -> String)
  -> List b
  -> FilePath
  -> { ...module, name :: a } -> PathResult
tryItemByKind = (constructor, getName, items, path, module) => pipe(
  List.find((e) => Just(getName(e)) == List.last(split("/", path))),
  where
    is Just found: constructor(module.name, found)
    is Nothing   : NotFound
)(items)


alias Finder = FilePath -> Module -> PathResult

_findItem :: List Finder -> FilePath -> Module -> PathResult
_findItem = (finders, path, module) => where(finders) {
  is [try, ...others]:
    where(try(path, module)) {
      is NotFound: _findItem(others, path, module)
      is found   : found
    }

  is []: NotFound
}

findItem :: FilePath -> Module -> PathResult
findItem = (path, module) => _findItem([
  tryItemByKind(ExpressionResult, .name, module.expressions),
  tryItemByKind(TypeResult, .name, module.typeDeclarations),
  tryItemByKind(AliasResult, .name, module.aliases),
  tryItemByKind(InterfaceResult, .name, module.interfaces),
  tryItemByKind(InstanceResult, .declaration, module.instances)
], path, module)


processPath :: State -> PathResult
export processPath = (state) => pipe(
  getModulesToShow,
  ifElse(
    isItemView(state.path),
    pipe(
      List.first,
      where is Just m: findItem(state.path, m)
    ),
    ModuleResult
  )
)(state)
